/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.gradleup.shadow'
    id 'com.diffplug.spotless' version '6.22.0'
}

dependencies {
    api(project(":ppl")) {
        exclude module: 'protocol'
    }

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: "${mockito_version}"
    testImplementation group: 'org.apache.calcite', name: 'calcite-testkit', version: '1.38.0'
}

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**', 'src/main/gen/**'
        }
        importOrder()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        googleJavaFormat('1.17.0').reflowLongStrings().groupArtifact('com.google.googlejavaformat:google-java-format')
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
    destinationDirectory = file("${project.buildDir}/distributions")
    archiveClassifier.set(null)

    exclude 'META-INF/maven/com.google.guava/**'
    exclude 'com/google/thirdparty/**'

    relocate 'com.google.common', 'shaded.com.google.common'
    relocate 'org.joda.time', 'shaded.org.joda.time'
}

publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            artifact shadowJar

            pom {
                name = 'opensearch-query-engine-api'
                description = 'OpenSearch SQL/PPL query engine API'
                groupId = "org.opensearch"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'OpenSearch'
                        url = 'https://github.com/opensearch-project/sql'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "Snapshots" //  optional target repository name
            url = "https://aws.oss.sonatype.org/content/repositories/snapshots"
            credentials {
                username "$System.env.SONATYPE_USERNAME"
                password "$System.env.SONATYPE_PASSWORD"
            }
        }
    }
}